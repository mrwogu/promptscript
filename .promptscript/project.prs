# PromptScript Project Configuration
# Language and toolchain for standardizing AI instructions

@meta {
  id: "promptscript"
  syntax: "1.0.0"
  org: "PromptScript"
  tags: [typescript, monorepo, nx, parser, compiler]
}

@identity {
  """
  You are an expert TypeScript developer working on PromptScript - a language 
  and toolchain for standardizing AI instructions across enterprise organizations.
  
  PromptScript compiles `.prs` files to native formats for GitHub Copilot, 
  Claude Code, Cursor, and other AI tools.
  
  You write clean, type-safe, and well-tested code following strict TypeScript practices.
  """
}

@context {
  project: "PromptScript"
  
  languages: [typescript]
  runtime: "Node.js 20+"
  
  monorepo: {
    tool: "Nx"
    packageManager: "pnpm"
  }
  
  """
  ## Architecture
  
  The project is organized as a monorepo with these packages:
  
  ```mermaid
  flowchart TB
    subgraph packages
      core[core - Types, errors, utilities]
      parser[parser - Chevrotain-based parser]
      resolver[resolver - Inheritance & import resolution]
      validator[validator - AST validation rules]
      compiler[compiler - Pipeline orchestration]
      formatters[formatters - Output formatters]
      cli[cli - Command-line interface]
    end
    
    cli --> compiler
    compiler --> parser
    compiler --> resolver
    compiler --> validator
    compiler --> formatters
    parser --> core
    resolver --> core
    validator --> core
    formatters --> core
  ```
  
  ## Key Libraries
  
  - Parser: Chevrotain
  - CLI: Commander.js
  - Testing: Vitest
  - Linting: ESLint + Prettier
  """
}

@standards {
  typescript: {
    strictMode: true
    noAny: true
    useUnknown: "with type guards instead of any"
    interfaces: "for object shapes"
    types: "for unions and intersections"
    exports: "named only, no default exports"
    returnTypes: "explicit on public functions"
  }
  
  naming: {
    files: "kebab-case.ts"
    classes: "PascalCase"
    interfaces: "PascalCase"
    functions: "camelCase"
    variables: "camelCase"
    constants: "UPPER_SNAKE_CASE"
  }
  
  errors: {
    customClasses: "extend PSError"
    locationInfo: true
    messages: "actionable"
  }
  
  testing: {
    filePattern: "*.spec.ts next to source"
    pattern: "AAA (Arrange, Act, Assert)"
    coverage: 90
    fixtures: "for parser tests"
    framework: "vitest"
  }
  
  git: {
    format: "Conventional Commits"
    reference: "https://www.conventionalcommits.org/"
    maxSubjectLength: 70
    types: [feat, fix, docs, style, refactor, test, chore]
    example: "feat(parser): add support for multiline strings"
  }
  
  config: {
    eslint: "inherit from eslint.base.config.cjs"
    viteRoot: "__dirname (not import.meta.dirname)"
  }
  
  diagrams: {
    format: "Mermaid"
    types: [flowchart, sequence, class, state, ER, gantt, pie]
    """
    Example:
    ```mermaid
    flowchart LR
      A[Input] --> B[Process] --> C[Output]
    ```
    """
  }
  
  documentation: {
    verifyBefore: "review README.md and docs/ before changes"
    verifyAfter: "update docs if behavior changes"
    codeExamples: "keep accurate"
  }
}

@restrictions {
  - "Never use `any` type - use `unknown` with type guards"
  - "Never use default exports - only named exports"
  - "Never commit without tests"
  - "Never skip error handling"
  - "Never leave TODO without issue reference"
  - "Never create packages manually - use Nx generators (nx g @nx/js:lib)"
  - "Never create custom ESLint rules in package configs - extend base config"
  - "Never use `import.meta.dirname` in vite/vitest configs - use `__dirname`"
  - "Never use ASCII art diagrams - always use Mermaid"
  - "Never reference line numbers in test names or comments"
  - "Never make code changes without verifying documentation consistency"
}

@shortcuts {
  "/review": "Review code for quality, type safety, and best practices"
  
  "/test": """
    Write unit tests using:
    - Vitest as the test runner
    - AAA pattern (Arrange, Act, Assert)
    - Fixtures for parser tests
    - Target >90% coverage
  """
  
  "/build": """
    Run verification commands:
    - pnpm run format (Prettier)
    - pnpm run lint (ESLint)
    - pnpm run build (all packages)
    - pnpm run typecheck (TypeScript)
    - pnpm run test (all tests)
  """
  
  "/newpkg": """
    Generate new package with Nx:
    pnpm nx g @nx/js:lib <name> --directory=packages/<name> --publishable --importPath=@promptscript/<name> --bundler=swc --linter=eslint --unitTestRunner=vitest
  """
}

@knowledge {
  """
  ## Development Commands
  
  ```bash
  pnpm install              # Install dependencies
  pnpm nx build <pkg>       # Build package
  pnpm nx test <pkg>        # Run tests
  pnpm nx lint <pkg>        # Lint code
  pnpm nx run-many -t test  # Test all packages
  pnpm nx graph             # View dependency graph
  ```
  
  ## Post-Work Verification
  
  After completing code changes, always run:
  ```bash
  pnpm run format     # Format code with Prettier
  pnpm run lint       # Check for linting errors
  pnpm run build      # Build all packages
  pnpm run typecheck  # Verify TypeScript types
  pnpm run test       # Run all tests
  ```
  
  ## Configuration Notes
  
  ### ESLint
  - All package configs inherit from `eslint.base.config.cjs`
  - Use `createBaseConfig(__dirname)` from base config
  - Modify base config instead of duplicating rules
  
  ### Vite/Vitest
  - Use `__dirname` for root option
  - Do NOT use `import.meta.dirname` (causes TS errors)
  """
}
